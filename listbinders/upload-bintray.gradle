apply plugin: 'com.jfrog.bintray'

def libVersion = "0.2"
version = libVersion

if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    publications = ['maven']
    configurations = ['archives']
    pkg {
        repo = "list-binders"
        name = "com.liusbl.libraries:listbinders"
        desc = "A RecyclerView wrapper providing an easy solution for complicated lists"
        vcsUrl = "https://github.com/liusbl/List-Binders"
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
        version {
            name = libVersion
            vcsTag = libVersion
            released = new Date()
        }
//        version {
//            desc = "A RecyclerView wrapper providing an easy solution for complicated lists"
//            gpg {
//                sign = true //Determines whether to GPG sign the files. The default is false
//                passphrase = properties.getProperty("bintray.gpg.password")
//                //Optional. The passphrase for GPG signing'
//            }
//        }
    }
}